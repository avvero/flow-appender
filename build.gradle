apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

repositories {
    mavenCentral()
}

dependencies {
    compile 'ch.qos.logback:logback-core:1.1.3'
    compile 'ch.qos.logback:logback-classic:1.1.3'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.6.3'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'flow-appender'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'Socket appender for Flow https://github.com/avvero/flow'
                url 'https://github.com/avvero/flow-appender'

                scm {
                    connection 'scm:git:git://github.com/avvero/flow-appender.git'
                    developerConnection 'scm:git:ssh://github.com:avvero/flow-appender.git'
                    url 'https://github.com/avvero/flow-appender/tree/master'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'avvero'
                        name 'Avvero Abernathy'
                        email 'avvero.abernathy@gmail.com'
                    }
                }
            }
        }
    }
}